#!/usr/bin/emacs --script
;;; aiernupate.el -- Publishing script for aiern

;;; Header:

;; This file is not part of Emacs

;; Author: Frank Fischer <frank-fischer@shadow-soft.de>
;; Maintainer: Frank Fischer <frank-fischer@shadow-soft.de>
;; Version: 1.0
;; Package-Requires: ((emacs "24.3") (s "1.9.0") (f "0.17.2") yaoddmuse)

;; The contents of this file are subject to the GPL License, Version 3.0.

;; Copyright (C) 2015, 2016, Frank Fischer

;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:

;; This is a small script to update aiern's version at certain places:
;;
;;  - topic of the IRC channel #aiern-mode in irc.libera.chat
;;  - EmacsWiki page
;;  - header of all source files
;;  - the ELPA package file
;;
;; The script should be invoked with the new version string
;;
;;   aiern-update.el 1.2.3
;;

;;; Code:

(package-initialize)

(require 'f)
(require 'erc)
(require 'yaoddmuse)

(defconst aiernupdate-irc-nick "lyrobot")
(defconst aiernupdate-irc-channel "#aiern-mode")
(defconst aiernupdate-irc-topic
  "aiern = Extensible vi layer for Emacs | Current version: VERSION | https://github.com/emacs-aiern/aiern | If you don't get an answer here, please use the mailing-list implementations-list@lists.ourproject.org")
(defconst aiernupdate-wiki-page "aiern")
(defvar aiernupdate-version "1.0.0")
(defvar aiernupdate-done nil)

(defun aiernupdate-wait ()
  "Wait until `aiernupdate-done' is set to t."
  (while (not aiernupdate-done)
    (sleep-for 1))
  (setq aiernupdate-done nil))

(defun aiernupdate-done ()
  "Set `aiernupdate-done' to t to stop the corresponding `aiernupdate-wait'."
  (setq aiernupdate-done t))

(defun aiernupdate-update-irc ()
  "Update the topic of the IRC channel."
  (let ((erc-nick aiernupdate-irc-nick)
        (erc-autojoin-channels-alist nil)
        (auth-sources nil))
    (erc-tls :server "irc.libera.chat" :port "6697" :nick aiernupdate-irc-nick)
    (add-hook 'erc-join-hook
              (lambda ()
                (let (case-fold-search)
                  (erc-set-topic (replace-regexp-in-string "VERSION"
                                                           aiernupdate-version
                                                           aiernupdate-irc-topic
                                                           nil t)))
                (erc-quit-server "")
                (sleep-for 5)
                (aiernupdate-done)))
    (erc-join-channel aiernupdate-irc-channel)
    (aiernupdate-wait)))

(defun aiernupdate-wiki-trace (page-buffer-name)
  "Callback for updating the EmacsWiki page in buffer PAGE-BUFFER-NAME."
  (with-current-buffer page-buffer-name
    (goto-char (point-min))
    (when (re-search-forward "Current stable version: \\*\\*\\([^*]*\\)\\*\\*")
      (goto-char (match-beginning 1))
      (delete-region (match-beginning 1) (match-end 1))
      (insert aiernupdate-version)
      (yaoddmuse-post-buffer (current-buffer) aiernupdate-version nil)
      (aiernupdate-done))))

(unless (require 'nadvice nil t)
  (defadvice yaoddmuse-display-page (around aiernupdate activate)
    (aiernupdate-wiki-trace (ad-get-arg 0))))

(defun aiernupdate-update-emacswiki ()
  "Update the version information on the EmacsWiki page."
  (setq yaoddmuse-wikis
        '(("EmacsWiki" "https://www.emacswiki.org/emacs" utf-8 "uihnscuskc=1;")))
  (when (featurep 'nadvice)
    (advice-add 'yaoddmuse-display-page :after 'aiernupdate-wiki-trace))
  (yaoddmuse-edit "EmacsWiki" aiernupdate-wiki-page t)
  (aiernupdate-wait))

(defun aiernupdate-update-regexp (file regexp)
  "Update version tag in FILE.
The version tag is defined by the REGEXP, in particular by the first
match group."
  (find-file file)
  (goto-char (point-min))
  (when (re-search-forward regexp nil t)
    (goto-char (match-beginning 1))
    (delete-region (match-beginning 1) (match-end 1))
    (insert aiernupdate-version)
    (save-buffer)))

(defun aiernupdate-update-files ()
  "Replace version comments in all source files."
  (dolist (file (f-glob "*.el"))
    (aiernupdate-update-regexp file ";;\\s-*Version:\\s-*\\([^\n]+\\)")))

(defun aiernupdate-update-pkg ()
  "Replace version in package file."
  (aiernupdate-update-regexp "aiern-pkg.el" "\"aiern\"[ \t\n\r]*\"\\([^\"]+\\)\""))

(defun aiernupdate-update-var ()
  "Replace version in `aiern-version' variable."
  (aiernupdate-update-regexp
   "aiern-vars.el"
   "t \"\\([^\"]*\\)\"[) \n\r\t]*\"The current version of aiern"))

(defun aiernupdate-update-news ()
  "Send an posting to the mailing list."
  (shell-command (concat "thunderbird -compose \"newsgroups=gmane.emacs.vim-emulation,preselectid=id2,subject='[ANN] aiern version " aiernupdate-version " released',body='"
                         "aiern version " aiernupdate-version " has been released.

The stable version can be installed from MELPA stable [1].
The latest development version is available from MELPA [2].

The aiern developers.

[1] http://stable.melpa.org
[2] http://melpa.org
'\"")))

(defun aiernupdate-update (version nonews)
  "Update aiern to VERSION.
If NONEWS is non-nil do not post a message to the newsgroup."
  (setq aiernupdate-cnt 0)
  (setq aiernupdate-version version)
  (aiernupdate-update-emacswiki)
  (aiernupdate-update-irc)
  (aiernupdate-update-files)
  (aiernupdate-update-pkg)
  (aiernupdate-update-var)
  (unless nonews (aiernupdate-update-news)))

(cond
 ((= (length argv) 2)
  (unless (equal (car argv) "nonews")
    (user-error "Usage: aiernupdate.el [-n] VERSION"))
  (aiernupdate-update (cadr argv) t))
 ((= (length argv) 1)
  (aiernupdate-update (cadr argv)))
 (t
  (user-error "Usage: aiernupdate.el [-n] VERSION")))

;;; aiernupdate.el ends here
